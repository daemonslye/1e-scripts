!script {{

--/|Script Name: Special-Abilities
--/|Location: CharacterScripts

--/|Version: 2023.05.22.001 // adding an emote

--/|SETTINGS SECTION
--/|==============================================================================================================



  --/|FORMATTING SECTION
  --/|==============================================================================================================

  --#overridetemplate|VileDarkness_TextButtons

    --#emoteBackground|#f2f2f3
    --#emoteFont|"Contrail One"
    --#emoteFontColor|#4d1635
    --#emoteFontWeight|regular
    --#emoteFontSize|16px

  --/|BUTTON FORMAT (seems to work best with sheetbutton or rbutton)
    --#buttonBackground|#4d1635
    --#buttonFontFace|Tahoma
    --#buttonFontSize|14px

--/|SCRIPT SECTION
--/|==============================================================================================================

--#reentrant|Special_Abilities_Menu_@{selected|character_id}

--&ABILITYSCRIPTMULE|AbilityScripts

  --#whisper|self
  --#title|@{selected|token_name}'s Special Abilities
  --#emoteState|invisible

  --#sourceToken|@{selected|token_id}
  --&SelectedID|[*S:character_id]

  --Rfirst|@{selected|character_id};repeating_ability

  --=Count|1
  --&Buttons|

      --:SpecialAbilityButtonDisplay|
      --?"[*R:ability_name]" -eq "NoRepeatingAttributeLoaded"|SkipAbility
      --?"X[*R:ability_name]" -eq X|SkipAbility

      --&NumUses|[*R:ability_current]
      --&UsesMax|[*R:ability_current^]
      --&Slash|/

      --?[&NumUses] -eq u|[
            --&Slash|
            --&UsesMax|
            --&NumUses|U
            --]|

      --?"[*R:ability_description]" -inc "AbilityScripts"|MakeSheetButton
      --&Button|[rbutton][*R:ability_name] ([&NumUses][&Slash][&UsesMax])::USE_ABILITY;[=[$Count.Raw] - 1][/rbutton]
      --^SkipSheetButton|
      --:MakeSheetButton|
      --&Button|[button][*R:ability_name] ([&NumUses][&Slash][&UsesMax])::~[&ABILITYSCRIPTMULE]|[*R:ability_name][/button]
      --:SkipSheetButton|
      --&PrintButtons|+ [&1stRowFormat][&Button][&EndRow]
      --=Count|[$Count] + 1
      --^SkipAddCount|
      --:SkipAbility|
      --=Count|[$Count] + 1
      --:SkipAddCount|
      --Rnext|
      --?"[*R:ability_name]" -ne "NoRepeatingAttributeLoaded"|SpecialAbilityButtonDisplay
      --+|[&PrintButtons]
      --X|

  --:USE_ABILITY|

  --#whisper|
  --#emoteState|visible

  --/|USE BUTTON VALUES

  --/|Split the reentryval parameters into an array

  --/|1=Ability Row
  --~params|string;split;|;[&reentryval]

  --=AbilityRow|[&params1]
  --Rbyindex|@{selected|character_id};repeating_ability;[$AbilityRow]

  --&AbilityName|
  --&AbilityName|+[*R:ability_name]

  --?[*R:ability_current] -eq u|SkipError
  --=NumUsesDecimal|[*R:ability_current]
  --?"[$NumUsesDecimal]" -lt 1|ERROR
  --:SkipError|

  --?"[*S:t-tooltip]" -inc "pc-control"|SkipWhisper|
  --?[*S:is_npc] -eq 1|&WhisperTo;gm
  --:SkipWhisper|

  --#whisper|[&WhisperTo]

  --=Roll|[*R:ability_die] + [*R:ability_mod]
  --&RollLine|[b]Adjusted Roll:[/b] [$Roll]
  --?[*R:ability_die] -eq x|&RollLine;[b]Roll:[/b] No Roll Needed

  --=CurrentUses|[*R:ability_current] - 1

  --&Uses|[$CurrentUses] of [*R:ability_current^]
  --?[*R:ability_current] -eq u|[
        --&Uses|Unlimited
        --^SkipDecrement|
        --]|

    --@setattr|_charid [*S:character_id] _modb _[*R>ability_current]|-1 _silent
    --:SkipDecrement|

  --:PRINT|
  --?"[&AbilityName]" -eq Educated|&AbilityName;an Educated Check
  --#emoteText|[*S:t-name] uses [&AbilityName]!
  --?"[&AbilityName]" -inc Educated|&AbilityName;Educated Check
  --#title|[&AbilityName]
  --+|[h1][&RollLine][&EndRow]
  --+|[h2][b]Uses Left:[/b] [&Uses]
  --+|[h3][b]DESCRIPTION[/b]
  --+|[h1][*R:ability_description]
  --+|[h2][b]LINK:[/b] [*R:ability_short_description]
  --X|

  --:ERROR|
  --#title|[&AbilityName]
  --&RollLine|[b]There are no remaining uses of this ability![/b]
  --+|[h1][&RollLine]
  --X|

  }}

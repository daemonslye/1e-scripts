!script {{
--/|Script Name: Cast-Spell
--/|Location: CharacterScripts
--/|Version: 2023.07.12.001 // fixed caster level bug
--/|SETTINGS =====================================================================================================
--&TOKEN_HP_BAR|bar1
--&Immobile_Marker|immobile::5465267
--/|FORMATTING ===================================================================================================
--#overridetemplate|VileDarkness_PerDay
--#buttonBackground|#4d1635
--#buttonFontFace|Tahoma
--#buttonFontSize|14px
--/|SCRIPT =======================================================================================================

--&SPELLSCRIPTSMULE|SpellScripts

+++snlib+++
--#reentrant|@{selected|token_id}_VileDarkness_Cast_Spell
--#whisper|self
--#emoteState|1
--#sourceToken|@{selected|token_id}
--&SelectedID|[*S:character_id]
--#title|[*S:t-name]'s Prepared Spells/Abilities

--+|[h1][b]Prepared Spells[/b]

--/|ALIGNMENT CHECK
--&Alignment|good
--?"[*S:alignment]" -inc ne -and "[*S:alignment]" -ninc neutral|&Alignment;evil
--?"[*S:alignment]" -inc ce|&Alignment;evil
--?"[*S:alignment]" -inc le|&Alignment;evil
--?"[*S:alignment]" -inc evil|&Alignment;evil
--?[&Alignment] -eq good|&CURE_BUTTON_TYPE;Cure|&CURE_BUTTON_TYPE;Cause

--rFirst|@{selected|character_id};repeating_spells
--=Spell_Row|0
--:Spell_Loop|

--&Spell_Number_Prepared|[*R:spell_memorized]
--~SPELL_NUM|string;onlynumbers;[&Spell_Number_Prepared]
--=Number_Prepared|[&SPELL_NUM]

--?"[*R:spell_school]" -inc SLA -and [&Spell_Number_Prepared] -inc u|Create_SLA_Button
--?[$Number_Prepared] -lt 1|Skip_Name
--?"[*R:spell_school]" -inc SLA|Create_SLA_Button
--?"[*R:spell_school]" -inc SCROLL|Create_Scroll_Button
--?"[*R:spell_school]" -inc ITEM|Create_Item_Button

--/|SPELL BUTTONS
--%LoopCounter|1;[$Number_Prepared];1
--&CURE_BUTTON|
--&CURE_SPELL|
--&VIEW_BUTTON|[rbutton](View)::VIEW_SPELL;[$Spell_Row]|[/rbutton]
--?"[*R:spell_school]" -inc cleric|[
    --&CURE_BUTTON|[rbutton]([&CURE_BUTTON_TYPE])::CAST_SPELL;[$Spell_Row]|&#64;{target|token_id}[/rbutton]
    --]|
-->CHECK_FOR_CURE|
--?"[*R:spell_description]" -inc SpellScripts|Make_Spell_Sheet_Button
--&SpellButton|[rbutton][*R:spell_name]::CAST_SPELL;[$Spell_Row]|[&CURE_SPELL][/rbutton][&CURE_BUTTON][&VIEW_BUTTON][br]
--^Skip_Spell_Sheet_Button|
--:Make_Spell_Sheet_Button|
--/|SpellButton|[sheetbutton][*R:spell_name]::SpellScripts::[*R:spell_name][/sheetbutton][&CURE_BUTTON][&VIEW_BUTTON][br]
--&SpellButton|[button][*R:spell_name]::~[&SPELLSCRIPTSMULE]|[*R:spell_name][/button][&CURE_BUTTON][&VIEW_BUTTON][br]
--:Skip_Spell_Sheet_Button|

--&SpellButtons[*R:spell_level]|+ [&SpellButton]
--%|
--^Skip_Name|

--/|SLA BUTTONS
--:Create_SLA_Button|

--&CURE_SPELL|
--&VIEW_BUTTON|[rbutton](View)::VIEW_SPELL;[$Spell_Row]|[/rbutton]
-->GET_CASTER_LEVEL|
-->GET_NUMBER_OF_USES|
-->CHECK_FOR_CURE|
--?"[*R:spell_description]" -inc SpellScripts|Make_SLA_Sheet_Button
--&SLA_BUTTON|+ [rbutton][*R:spell_name] [&SLA_Level]::CAST_SPELL;[$Spell_Row]|[&CURE_SPELL][/rbutton][&VIEW_BUTTON] [b][&SLA_USES_TEXT][/b][br]
--^Skip_SLA_Sheet_Button|
--:Make_SLA_Sheet_Button|
--&SLA_BUTTON|+ [sheetbutton][*R:spell_name]::SpellScripts::[*R:spell_name][/sheetbutton][&VIEW_BUTTON] [b][&SLA_USES_TEXT][/b][br]
--:Skip_SLA_Sheet_Button|
--^Skip_Name|

--/|SCROLL BUTTONS
--:Create_Scroll_Button|
--&CURE_SPELL|
--&VIEW_BUTTON|[rbutton](View)::VIEW_SPELL;[$Spell_Row]|[/rbutton]
-->GET_CASTER_LEVEL|
-->CHECK_FOR_CURE|
--?"[*R:spell_description]" -inc SpellScripts|Make_Scroll_Sheet_Button
--&SCROLL_BUTTON|+ [rbutton][*R:spell_name] [&SLA_Level]::CAST_SPELL;[$Spell_Row]|[&CURE_SPELL][/rbutton][&VIEW_BUTTON] [b]x[*R:spell_memorized][/b][br]
--^Skip_Scroll_Sheet_Button|
--:Make_Scroll_Sheet_Button|
--&SCROLL_BUTTON|+ [sheetbutton][*R:spell_name]::SpellScripts::[*R:spell_name][/sheetbutton][&VIEW_BUTTON] [b]x[*R:spell_memorized][/b][br]
--:Skip_Scroll_Sheet_Button|

--^Skip_Name|

--/|ITEM ABILITY BUTTONS
--:Create_Item_Button|
--&CURE_SPELL|
--&VIEW_BUTTON|[rbutton](View)::VIEW_SPELL;[$Spell_Row]|[/rbutton]
-->GET_CASTER_LEVEL|
-->CHECK_FOR_CURE|
-->GET_CHARGES|
--?"[*R:spell_description]" -inc SpellScripts|Make_Item_Sheet_Button
--&ITEM_BUTTON|+ [rbutton][*R:spell_name] [&SLA_Level]::CAST_SPELL;[$Spell_Row]|[&CURE_SPELL][/rbutton][&VIEW_BUTTON] [&CHARGES_TEXT][br]
--^Skip_Item_Sheet_Button|
--:Make_Item_Sheet_Button|
--&ITEM_BUTTON|+ [sheetbutton][*R:spell_name]::SpellScripts::[*R:spell_name][/sheetbutton][&VIEW_BUTTON] [&CHARGES_TEXT][br]
--:Skip_Item_Sheet_Button|

--^Skip_Name|

--:Skip_Name|
--rNext|
--=Spell_Row|[$Spell_Row] + 1
--?"[*R:spell_name]" -ne "NoRepeatingAttributeLoaded"|Spell_Loop

--?"X[&SpellButtons1]" -eq X|Skip_Level_1
--+|[h2][b]1st Level[/b]
--+|[&SpellButtons1]
--:Skip_Level_1|
--?"X[&SpellButtons2]" -eq X|Skip_Level_2
--+|[h2][b]2nd Level[/b]
--+|[&SpellButtons2]
--:Skip_Level_2|
--?"X[&SpellButtons3]" -eq X|Skip_Level_3
--+|[h2][b]3rd Level[/b]
--+|[&SpellButtons3]
--:Skip_Level_3|
--?"X[&SpellButtons4]" -eq X|Skip_Level_4
--+|[h2][b]4th Level[/b]
--+|[&SpellButtons4]
--:Skip_Level_4|
--?"X[&SpellButtons5]" -eq X|Skip_Level_5
--+|[h2][b]5th Level[/b]
--+|[&SpellButtons5]
--:Skip_Level_5|
--?"X[&SpellButtons6]" -eq X|Skip_Level_6
--+|[h2][b]6th Level[/b]
--+|[&SpellButtons6]
--:Skip_Level_6|
--?"X[&SpellButtons7]" -eq X|Skip_Level_7
--+|[h2][b]7th Level[/b]
--+|[&SpellButtons7]
--:Skip_Level_7|
--?"X[&SpellButtons8]" -eq X|Skip_Level_8
--+|[h2][b]8th Level[/b]
--+|[&SpellButtons8]
--:Skip_Level_8|
--?"X[&SpellButtons9]" -eq X|Skip_Level_9
--+|[h2][b]9th Level[/b]
--+|[&SpellButtons9]
--:Skip_Level_9|


--?"X[&SCROLL_BUTTON]" -eq X|Skip_Scroll_Buttons
--+|[h2][b]Scroll Spells[/b]
--+|[&SCROLL_BUTTON]
--:Skip_Scroll_Buttons|
--?"X[&SLA_BUTTON]" -eq X|Skip_SLA_Buttons
--+|[h2][b]Spell-like Abilities[/b]
--+|[&SLA_BUTTON]
--:Skip_SLA_Buttons|
--?"X[&ITEM_BUTTON]" -eq X|Skip_Item_Buttons
--+|[h2][b]Item Abilities[/b]
--+|[&ITEM_BUTTON]
--:Skip_Item_Buttons|
--X|

--:GET_CASTER_LEVEL|
--?"[*R:spell_school]" -inc ITEM|Set_Caster_Level_Non-Spell
--?"[*R:spell_school]" -inc SLA|Set_Caster_Level_Non-Spell
--?"[*R:spell_school]" -inc SCROLL|Set_Caster_Level_Non-Spell

--?"[*R:spell_school]" -inc magic|=Caster_Level;[*S:spells_lvl_0^]
--?"[*R:spell_school]" -inc illusionist|=Caster_Level;[*S:spells_lvl_0^]
--?"[*R:spell_school]" -inc cleric|=Caster_Level;[*S:spells_lvl_0_current]
--?"[*R:spell_school]" -inc druid|=Caster_Level;[*S:spells_lvl_0_current]

--^Skip_Set_Caster_Level|

--:Set_Caster_Level_Non-Spell|
--&SLA_Level|[*R:spell_school]
--&SLA_Level|[&SLA_Level(after,:)]
--&SLA_Level|([&SLA_Level])
--~Caster_Level_Check|string;onlynumbers;[&SLA_Level]
--=Caster_Level|[&Caster_Level_Check]

--:Skip_Set_Caster_Level|

--<|

--:GET_NUMBER_OF_USES|
--&Number_of_Uses_Raw|[*R:spell_memorized]

--~SLA_USES|string;onlynumbers;[&Number_of_Uses_Raw]
--&SLA_USES_TEXT|[&SLA_USES]/day
--?"[&Number_of_Uses_Raw]" -inc u|&SLA_USES_TEXT;At-will
--?"[&Number_of_Uses_Raw]" -inc d|&SLA_USES_TEXT;[&SLA_USES]/day
--?"[&Number_of_Uses_Raw]" -inc w|&SLA_USES_TEXT;[&SLA_USES]/week

--<|

--:CHECK_FOR_CURE|
--?"[*R:spell_name]" -inc cure -and "[*R:spell_name]" -inc wounds|[
    --&CURE_SPELL|&#64;{target|token_id}
    --]|
--?"[*R:spell_name]" -inc cause -and "[*R:spell_name]" -inc wounds|[
    --&CURE_SPELL|&#64;{target|token_id}
    --]|

--?"[*R:spell_name]" -eq Heal|[
    --&CURE_SPELL|&#64;{target|token_id}
    --]|

--?"[*R:spell_name]" -eq Harm|[
    --&CURE_SPELL|&#64;{target|token_id}
    --]|
--<|

--:GET_CHARGES|
--&Number_of_Charges_Raw|[*R:spell_memorized]
--~ITEM_CHARGES|string;onlynumbers;[&Number_of_Charges_Raw]
--&CHARGES_TEXT|[b]Chgs: [&ITEM_CHARGES][/b]
--?[&Number_of_Charges_Raw] -eq u|&CHARGES_TEXT;[b]Chgs: Unlimited[/b]
--?[&Number_of_Charges_Raw] -inc d|&CHARGES_TEXT;[b]Uses: [&ITEM_CHARGES]/day[/b]
--?[&Number_of_Charges_Raw] -inc w|&CHARGES_TEXT;[b]Uses: [&ITEM_CHARGES]/week[/b]
--<|

--:CAST_SPELL|
--#whisper|
--~params|string;split;|;[&reentryval]
--=Cast_Spell_Row|[&params1]
--=Caster_Level|0

--?"X[&params2]" -ne X|>CAST_CURE;[&params1];[&params2]
--Rbyindex|@{selected|character_id};repeating_spells;[$Cast_Spell_Row]
-->GET_CASTER_LEVEL|

--?[$Caster_Level] -gt 0|Skip_Level_Checks
--?"[*R:spell_school]" -inc magic|=Caster_Level;[*S:spells_lvl_0^]
--?"[*R:spell_school]" -inc illusionist|=Caster_Level;[*S:spells_lvl_0^]
--?"[*R:spell_school]" -inc cleric|=Caster_Level;[*S:spells_lvl_0_current]
--?"[*R:spell_school]" -inc druid|=Caster_Level;[*S:spells_lvl_0_current]

--:Skip_Level_Checks|

-->ADD_EFFECT_TO_TRACKER|

--?"[*R:spell_school]" -inc SCROLL|&Added_Text;from a Scroll
--#title|[*S:t-name] casts [*R:spell_name] [&Added_Text]
--+|[h1][b]Spell Level:[/b] [*R:spell_level] (CL [$Caster_Level.Raw])
--+|[h2][b]CT:[/b] [*R:spell_ct] ([*R:spell_components])
--+|[h1][b]Range:[/b] [*R:spell_range]
--+|[h2][b]AOE:[/b] [*R:spell_aoe]
--+|[h1][b]Duration:[/b] [*R:spell_duration]
--+|[h2][b]Save:[/b] [*R:spell_save]
--+|[h1][*R:spell_description]

-->REMOVE_SPELL|
-->CLEAN_UP|

--X|

--:CAST_CURE|
--#whisper|
--=Cast_Spell_Row|[%1%]
--&Target_Token|[%2%]
--Rbyindex|@{selected|character_id};repeating_spells;[$Cast_Spell_Row]
--=Spell_Level|[*R:spell_level]
--=Cure_Dice|2
--?"[*R:spell_name]" -inc light|=Cure_Dice;2
--?"[*R:spell_name]" -inc serious|=Cure_Dice;4
--?"[*R:spell_name]" -inc critical|=Cure_Dice;6
--?[$Spell_Level] -gt 3|=Cure_Dice;4
--?[$Spell_Level] -gt 4|=Cure_Dice;6
--&Spell_Type|cure
--?[&CURE_BUTTON_TYPE] -eq Cure|&Spell_Type;cure
--?[&CURE_BUTTON_TYPE] -eq Cause|&Spell_Type;cause
--?"[*R:spell_name]" -inc cause|&Spell_Type;cause

--=Keep_Highest|[$Cure_Dice] / 2

--=Cure_Bonus|0

--?"X[$Caster_Level]" -eq X|=Caster_Level;[*S:spells_lvl_0_current]
--?[$Caster_Level] -lt 1|=Caster_Level;[*S:spells_lvl_0_current]
--?[*R:spell_level] -gt 1|=Cure_Bonus;[$Caster_Level]
--?"[*S:deity]" -inc pelor|=Cure_Bonus;[$Caster_Level]


--?"[*R:spell_name]" -eq Heal -or "[*R:spell_name]" -eq Harm|HEAL_HARM

--=Cure_Roll|[$Cure_Dice]d8kh[$Keep_Highest] + [$Cure_Bonus]
--=CURRENTHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_value]
--=MAXHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_max]
--=CURED_HP|[$CURRENTHP] + [$Cure_Roll] {MAX:[$MAXHP]}

--?[&Spell_Type] -eq cause|[
  --=CURED_HP|[$CURRENTHP] - [$Cure_Roll] {MAX:[$MAXHP]}
--]|


--^Skip_Heal_Harm|
--:HEAL_HARM|

--?"[*R:spell_name]" -eq Heal|[
  --=CURRENTHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_value]
  --=MAXHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_max]
  --=CURED_HP|[$MAXHP] - 1d4
  --#title|[*S:t-name] casts a Heal on [*[&Target_Token]:t-name]
  --+|[h1]Cured from [b][$CURRENTHP.Raw] HP[/b] to [b][$CURED_HP.Raw] HP[/b]
  --!t:[&Target_Token]|[&TOKEN_HP_BAR]_value:[$CURED_HP]
  --X|
--]|

--?"[*R:spell_name]" -eq Harm|[
  --=CURRENTHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_value]
  --=MAXHP|[*[&Target_Token]:t-[&TOKEN_HP_BAR]_max]
  --=CURED_HP|1d4
  --#title|[*S:t-name] casts a Heal on [*[&Target_Token]:t-name]
  --+|[h1]Harmed from [b][$CURRENTHP.Raw] HP[/b] to [b][$CURED_HP.Raw] HP[/b]
  --!t:[&Target_Token]|[&TOKEN_HP_BAR]_value:[$CURED_HP]
  --X|
--]|

--:Skip_Heal_Harm|

--!t:[&Target_Token]|[&TOKEN_HP_BAR]_value:[$CURED_HP]

--?[&Spell_Type] -eq cure|[
--#title|[*S:t-name] casts a Cure Wounds on [*[&Target_Token]:t-name]
--+|[h1]Cured from [b][$CURRENTHP.Raw] HP[/b] by [$Cure_Roll] HP to [b][$CURED_HP.Raw] HP[/b]
--+|[h2]Spell Used: [*R:spell_name]

--]|

--?[&Spell_Type] -eq cause|[
--#title|[*S:t-name] casts a Cause Wounds on [*[&Target_Token]:t-name]
--+|[h1]Damaged from [b][$CURRENTHP.Raw] HP[/b] by [$Cure_Roll] HP to [b][$CURED_HP.Raw] HP[/b]
--+|[h2]Spell Used: [*R:spell_name]
--]|

-->REMOVE_SPELL|
-->CLEAN_UP|

--X|

--:VIEW_SPELL|

--#overridetemplate|VileDarkness_Gray
--~params|string;split;|;[&reentryval]
--=Cast_Spell_Row|[&params1]
--Rbyindex|@{selected|character_id};repeating_spells;[$Cast_Spell_Row]
--#title|[*R:spell_name]
-->GET_CASTER_LEVEL|
--+|[h1][b]Spell Level:[/b] [*R:spell_level] (CL [$Caster_Level.Raw])
--+|[h2][b]CT:[/b] [*R:spell_ct] ([*R:spell_components])
--+|[h1][b]Range:[/b] [*R:spell_range]
--+|[h2][b]AOE:[/b] [*R:spell_aoe]
--+|[h1][b]Duration:[/b] [*R:spell_duration]
--+|[h2][b]Save:[/b] [*R:spell_save]
--+|[h1][*R:spell_description]

--X|

--:CLEAN_UP|
--!t:[*S:t-id]|tint_color:transparent
-->LibSN_REMOVE_STATUS_MARKER|[*S:t-id];[&Immobile_Marker]
--<|

--:REMOVE_SPELL|
--?[*R:spell_memorized] -inc d|&USE_PERIOD;d
--?[*R:spell_memorized] -inc w|&USE_PERIOD;w
--?[*R:spell_memorized] -inc c|Skip_Decrement
--?"[*R:spell_school]" -inc item|Skip_Decrement
--?[*R:spell_memorized] -inc u|Skip_Decrement
--~CURRENT_USES|string;onlynumbers;[*R:spell_memorized]
--=CURRENT_USES|[&CURRENT_USES]
--=NEW_USES|[$CURRENT_USES] - 1
--!a:[&SelectedID]|[*R>spell_memorized]:[$NEW_USES.Raw][&USE_PERIOD]
--*|[*S:t-name]'s [*R:spell_name] uses go from [$CURRENT_USES] to [$NEW_USES]
--:Skip_Decrement|

--?"[*R:spell_school]" -inc item -and [*R:spell_memorized] -inc d|[
--~CURRENT_USES|string;onlynumbers;[*R:spell_memorized]
--=CURRENT_USES|[&CURRENT_USES]
--=NEW_USES|[$CURRENT_USES] - 1
--!a:[&SelectedID]|[*R>spell_memorized]:[$NEW_USES.Raw]
--*|[*S:t-name]'s [*R:spell_name] uses go from [$CURRENT_USES] to [$NEW_USES]
--^Skip_Remove_Charges|
--]|

--?"[*R:spell_school]" -inc item|[
    --~Charges|string;onlynumbers;[*R:spell_memorized]
    --#overridetemplate|VileDarkness_StandardButtons
    --+|[h2][sheetbutton]Remove Charges ([&Charges] chgs)::CharacterScripts::Use-Magic-Item[/sheetbutton]
    --]|

--:Skip_Remove_Charges|

--<|

--:ADD_EFFECT_TO_TRACKER|
--&Add_to_Tracker|no
--?"[*R:spell_duration]" -inc rounds|&Add_to_Tracker;yes
--~NoNumbersDuration|string;nonumbers;[*R:spell_duration]
--?"[&NoNumbersDuration]" -eq r|&Add_to_Tracker;yes

--?"[*R:spell_duration]" -inc r|&Add_to_Tracker;yes
--?"[*R:spell_duration]" -inc hour|&Add_to_Tracker;no
--?"[*R:spell_duration]" -inc instant|&Add_to_Tracker;no
--?"[*R:spell_duration]" -inc perm|&Add_to_Tracker;no

--?[&Add_to_Tracker] -ne yes|Skip_Add_to_Tracker
--?"[&NoNumbersDuration]" -eq r|[
    --~DurationNumber|string;onlynumbers;[*R:spell_duration]
    --&Duration|[&DurationNumber]
    --^Skip_Duration_Question|
    --]|
--iSpell Duration appears to be: ([*R:spell_duration]);Enter Duration for [*R:spell_name]|q;Duration;Enter Spell Duration in Rounds|1
--:Skip_Duration_Question|
--~|turnorder;addcustom;[*S:t-name]-[*R:spell_name];[&Duration];-1

--:Skip_Add_to_Tracker|
--<|


}}

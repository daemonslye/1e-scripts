!script {{

--/|Script Name: Use-Magic-Item
--/|Location: CharacterScripts

--/|Version: 2023.06.12.001 // trying to change to buttons without initialize

--/|SETTINGS SECTION
--/|==============================================================================================================
--/|FORMATTING SECTION
--/|==============================================================================================================

--#overridetemplate|VileDarkness_TextButtons

  --#emoteBackground|#f2f2f3
  --#emoteFont|"Contrail One"
  --#emoteFontColor|#4d1635
  --#emoteFontWeight|regular
  --#emoteFontSize|16px

--/|BUTTON FORMAT
  --#buttonBackground|#4d1635
  --#buttonFontFace|Tahoma
  --#buttonFontSize|14px

--/|SCRIPT SECTION
--/|==============================================================================================================

--#reentrant|Magic_Items_Menu_@{selected|character_id}

  --#whisper|self
  --#title|@{selected|token_name}'s Magic Items
  --#emoteState|invisible

  --#sourceToken|@{selected|token_id}
  --&SelectedID|[*S:character_id]

  --&ITEMSCRIPTMULE|ItemScripts

  --=Count|1
  --&Buttons|

      --Rfirst|@{selected|character_id};repeating_equipment

      --:EquipmentButtonDisplay|
      --=TestCount|[$Count] - 1
      --/|[$TestCount]|[*R:equipment_item]

      --&PAREN1|(
      --&PAREN2|)

      --&NumUses|

      --?"[*R:equipment_item]" -eq "NoRepeatingAttributeLoaded"|SkipItem

      --?"[*R:equipment_type]" -ne 3|SkipItem
      --?[*R:equipment_current] -eq x|SkipItem
      --?"[*R:equipment_item]" -inc empty|SkipItem
      --?"X[*R:equipment_current]" -eq X|[
          --&PAREN1|
          --&PAREN2|
          --^SkipUses|
          --]|
      --?[*R:equipment_current] -eq 0|SkipUses

      --&NumUses|[*R:equipment_current]
      --&UsesMax|[*R:equipment_current^]
      --&Slash|/

      --?[&NumUses] -eq u|[
            --&Slash|
            --&UsesMax|
            --&NumUses|U
            --]|

      --&ButtonColor|
      --?"[*R:equipment_location]" -inc "Scroll-"|&ButtonColor;:#ffffff:#000000
      --:SkipUses|
      --?"[*R:equipment_description]" -inc "ItemScripts"|MakeItemSheetButton
      --?"[*R:equipment_description]" -inc "ItemAbilities"|MakeSpellSheetButton

      --&Button|[rbutton[&ButtonColor]][*R:equipment_item] [&PAREN1][&NumUses][&Slash][&UsesMax][&PAREN2]::USE_ITEM;[=[$Count.Raw] - 1][/rbutton]
      --^SkipSheetButton|

      --:MakeItemSheetButton|
      --&ScriptName|[*R:equipment_item]
      --&ButtonColor|:#000000:#F8C8DC
      --?"[*R:equipment_item]" -inc heal|&ScriptName;Healing-Potion
            --/|Button|[sheetbutton[&ButtonColor]][*R:equipment_item] [&PAREN1][&NumUses][&Slash][&UsesMax][&PAREN2]::ItemScripts::[&ScriptName][/sheetbutton]

            --&Button|[button][*R:equipment_item] [&PAREN1][&NumUses][&Slash][&UsesMax][&PAREN2]::~[&ITEMSCRIPTMULE]|[&ScriptName][/button]

      --^SkipSheetButton|

      --:MakeSpellSheetButton|
      
        --&Button|[button][*R:equipment_item] [&PAREN1][&NumUses][&Slash][&UsesMax][&PAREN2]::~[&ITEMSCRIPTMULE]|Use-Item-Abilities[/button]

      --^SkipSheetButton|

      --:SkipSheetButton|
      --&PrintButtons|+ [&Button]
      --=Count|[$Count] + 1
      --^SkipAddCount|
      --:SkipItem|
      --=Count|[$Count] + 1
      --:SkipAddCount|
      --Rnext|
      --?"[*R:equipment_item]" -ne "NoRepeatingAttributeLoaded"|EquipmentButtonDisplay
      --+|[&PrintButtons]

      --X|

      --:USE_ITEM|

      --&WHISPER_TO|
      --?[&SendingPlayerIsGM] -eq 1|&WHISPER_TO;self

      --#whisper|[&WHISPER_TO]

      --#emoteState|invisible

      --/|USE BUTTON VALUES

      --/|Split the reentryval parameters into an array

      --/|1=Item Row
      --~params|string;split;|;[&reentryval]

      --=ItemRow|[&params1]

      --Rbyindex|@{selected|character_id};repeating_equipment;[$ItemRow]

      --&RowToDelete|repeating_equipment_$[$ItemRow]

      --&ItemName|
      --&ItemName|+[*R:equipment_item]

      --#whisper|[&WHISPER_TO]

      --=ChargesUsed|1

      --?"[*R:equipment_item]" -inc heal|[
          --+|[h1]Please use Player-Menu > Add an Item to properly add a healing potion before using it.
          --X|
          --]|

      --?[*R:equipment_current] -eq u|[
          --^SkipUseCharges|
          --]|

      --?"[*R:equipment_location]" -ninc scroll|[
      --i;How Many Uses/Charges|q;ChargesUsed;How Many Uses/Charges to Use?|1
      --=ChargesUsed|[&ChargesUsed]

      --]|

      --=ItemCurrentUsesBefore|[*R:equipment_current]

      --&MaxUsesCheck|[*R:equipment_current^]
      --?[&MaxUsesCheck] -inc d|[
            --&Period|[&MaxUsesCheck]
            --^PerPeriod|
            --]|
            --?[&MaxUsesCheck] -inc w|[
                  --&Period|[&MaxUsesCheck]
                  --^PerPeriod|
                  --]|
                  --?[&MaxUsesCheck] -inc m|[
                        --&Period|[&MaxUsesCheck]
                        --^PerPeriod|
                        --]|

      --=ItemMaxUses|[*R:equipment_current^]
      --=ItemCurrentUses|[$ItemCurrentUsesBefore] - [$ChargesUsed]
      --&ItemUses|[$ItemCurrentUses] of [$ItemMaxUses]
      --^SkipPerPeriod|

      --:PerPeriod|
      --&DontDelete|yes
      --=ItemCurrentUses|[$ItemCurrentUsesBefore] - 1
      --&ItemUses|[$ItemCurrentUses] (per [&MaxUsesCheck])

      --:SkipPerPeriod|
      --:SkipUseCharges|

      --?[*R:equipment_current] -eq u|[
            --&ItemUses|Unlimited
            --^SkipDecrement|
            --]|

        --@setattr| _charid [*S:character_id] _modb _[*R>equipment_current]|-[$ChargesUsed] _silent

        --?[*R>equipment_current^] -eq u|SkipDeleteRow
        --?[&DontDelete] -eq yes|SkipDeleteRow

        --?"[*R:equipment_item]" -inc lsv -and [$ItemCurrentUses] -lt 1|[
            --@setattr| _charid [*S:character_id] _[*R>equipment_item]|Empty Leaded Steel Vial _silent
            --&LastRow|Your potion is gone and replaced by an empty leaded steel vial.
            --^SkipDeleteRow|
            --]|

        --?[$ItemCurrentUses] -lt 1|[
            --&OldItem|[*R:equipment_item]
            --@setattr| _charid [*S:character_id] _[*R>equipment_item]|Empty (was [&OldItem]) _[*R>equipment_type]|4 _silent
            --&LastRow|Your [*R:equipment_item] is listed as empty.
            --]|
        --:SkipDeleteRow|

        --:SkipDecrement|

      --#title|[*S:t-name]'s [&ItemName]
      --+|[h1][&RollLine]
      --+|[h2][b]Uses Left:[/b] [&ItemUses]
      --+|[h3][b]DESCRIPTION[/b]
      --+|[h2][*R:equipment_description]
      --+|[h1][&LastRow]
      --X|
}}

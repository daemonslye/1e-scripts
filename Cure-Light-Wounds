!script {{
  --/|Script Name: Cure-Light-Wounds-Spell
  --/|Location: SpellScripts

  --/|Note: This is for prepared spells only. Spontaneously cast cure spells are found in AbilityScripts.

  --/|Version: 2022.11.21.001

  --/|SETTINGS
  --/|==============================================================================================================

  --&TOKEN_HP_BAR|bar1

  --&SPELL_LINK|[+ DETAILS](https://sites.google.com/view/vile-spells/c-spells/cure-wounds-light-r)

  --/|FORMATTING SECTION
  --/|==============================================================================================================

  --#overridetemplate|VileDarkness_StandardButtons

  --#emoteText|
  --#emoteState|visible
  --#emoteBackground|#f2f2f3
  --#emoteFont|"Contrail One"
  --#emoteFontColor|#4d1635
  --#emoteFontWeight|regular
  --#emoteFontSize|16px

--/|BUTTON FORMAT (seems to work best with sheetbutton or rbutton)
  --#buttonBackground|#4d1635
  --#buttonFontFace|Tahoma
  --#buttonFontSize|14px

--/|SCRIPT
--/|==============================================================================================================

--&SpellName|Cure-Light-Wounds

--#sourceToken|@{selected|token_id}
--#targetToken|@{target|token_id}

--&CasterId|@{selected|character_id}
--&SelectedId|[*S:character_id]

--&SourceTokenName|[*S:t-name]
--&SourceTokenFirstName|[&SourceTokenName(word,1)]
--&TargetTokenName|[*T:t-name]
--&TargetTokenFirstName|[&TargetTokenName(word,1)]

--?"[&SourceTokenFirstName]" -eq The|&SourceTokenFirstName;[&SourceTokenName]
--?"[&TargetTokenFirstName]" -eq The|&TargetTokenFirstName;[&TargetTokenName]

--/|SET CASTER TYPES; Make sure to include all possible caster types for the spell limited to (list w/no spaces and separated by semi-colons): Magic-user;Illusionist;Cleric;Druid
--&PossibleCasterTypes|Cleric;Druid

-->FIND_CASTER_TYPE_AND_LEVEL|
-->IS_IT_PREPARED|

-->DEITY_CHECK|

--/|NOT USED
--/|CHECK_STATUS_MARKER - TokenID;MarkerToFind;ExistsVariable;CountVariable
--/|Will check the given token (TokenID) for the status marker (MarkerToFind) and set two return
--/|string variables: (ExistsVariable) will be 1 if the token has the marker, and (CountVariable)
--/|will contain the counter number on the marker (default 0 for no number)
--/|CHECK_STATUS_MARKER|@{target|token_id};[&Aid_Marker];ALREADY_THERE

--/|SPECIFIC SPELL CODE
--/|==============================================================================================================

--?[&Spell_Not_Found] -eq 1|SPELLNOTFOUND

--?[&CasterType] -inc Druid|=DruidBonus;[$CasterLevel]

--=CureRoll|2d8kh1 + [$DeityBonus][Deity] + [$DruidBonus][DRUID]
--=DURATION|
--=CURRENTHP|[*T:t-[&TOKEN_HP_BAR]_value]
--=MAXHP|[*T:t-[&TOKEN_HP_BAR]_max]
--=CURED_HP|[$CURRENTHP] + [$CureRoll] {MAX:[$MAXHP]}

--&FirstLine|Cured from [b][$CURRENTHP.Raw] HP[/b] by [$CureRoll] HP to [b][$CURED_HP.Raw] HP[/b]
--&SecondLine|[b]LINK:[/b] [&SPELL_LINK]
--?[&Spell_is_Not_Prepared] -eq 1|SPELL_NOT_PREPARED
--^PRINTCARD|

--:SPELL_NOT_PREPARED|
--#emoteText|[*S:t-name] attempts to cast [&SpellName]
--#title|[&SpellName]
--&FirstLine|[h5][c][b]It appears this spell is not prepared.[/b][/c]
--+|[h1][&FirstLine]
--X|

--:SPELLNOTFOUND|
--#emoteText|[*S:t-name] attempts to cast [&SpellName]
--#title|[&SpellName]
--&FirstLine|[h5][c][b]Cannot find [&SpellName] in your Spell List.[/b][/c]
--+|[h1][&FirstLine]
--X|

--:PRINTCARD|

--#emoteText|[&SourceTokenFirstName] cures [&TargetTokenFirstName]
--#title|[&SpellName] Spell (CL [$CasterLevel.Raw])

--+|[h1][&FirstLine]
--+|[h2][&SecondLine]

--/|TOKEN & SHEET ACTIONS
--!t:@{target|token_id}|[&TOKEN_HP_BAR]_value:[$CURED_HP]

--Rfind|[*S:character_id];[&SpellName];repeating_spells;spell_name
--@setattr| _charid [*S:character_id] _mod _[*R>spell_memorized]#-1 _silent

--X|

--/|SCRIPT FUNCTIONS
--/|==============================================================================================================

--:DEITY_CHECK|
--=DeityBonus|
--?"X[*S:deity]" -eq X|SkipDeityCheck
--?"[*S:deity]" -inc Pelor|=DeityBonus;[$CasterLevel]
--?"[*S:deity]" -inc Imhotep|&Special;1

--:SkipDeityCheck|
--<|

--:FIND_CASTER_TYPE_AND_LEVEL|

--/|If the sheet is set up as an NPC sheet, we need to ask for type and level
--?[*S:is_npc] -eq 1|[
      -->AskForCasterType|
      --^Skip_Caster_Checks|
      --]|

--Rfind|[&SelectedId];[&SpellName];repeating_spells;spell_name
--?"[*R:spell_name]" -eq NoRepeatingAttributeLoaded|[
      --&Spell_Not_Found|1
      --^Skip_Caster_Checks|
      --]|

--~|array;define;Possible_Caster_Types;[&PossibleCasterTypes]
--%loop|foreach;Possible_Caster_Types

--?"[*R:spell_description]" -inc [&loop]|[
      --&CasterType|[&loop]
      --%!|
      --]|
--%|

--?"[*S:class]" -inc [&CasterType]|=CasterLevel;[*S:level]
--?"[*S:secondclass]" -inc [&CasterType]|=CasterLevel;[*S:level_2]
--?"[*S:thirdclass]" -inc [&CasterType]|=CasterLevel;[*S:level_3]

--:Skip_Caster_Checks|
--<|

--:IS_IT_PREPARED|
--Rfind|[&SelectedId];[&SpellName];repeating_spells;spell_name
--?"X[*R:spell_memorized]" -eq X|NotPrepared
--?"[*R:spell_memorized]" -eq 0|NotPrepared
--&Spell_is_Not_Prepared|0
--^Spell_is_Good_to_Go|
--:NotPrepared|
--&Spell_is_Not_Prepared|1
--:Spell_is_Good_to_Go|
--<|

--:AskForCasterType|
--i;Enter Caster Level|q;CasterLevel;Select Caster Level|1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20||q;CasterType;Select Caster Type for the Spell/Effect|Cleric
--=CasterLevel|[&CasterLevel]
--&SkipPreparedCheck|1
--<|

}}
